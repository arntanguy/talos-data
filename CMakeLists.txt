CMAKE_MINIMUM_REQUIRED(VERSION 3.1)

# Project properties
SET(PROJECT_ORG pyrene-dev)
SET(PROJECT_NAME talos_data)
SET(PROJECT_DESCRIPTION "Talos urdf model, srdf model and other data")
SET(PROJECT_URL "https://gitlab.laas.fr/${PROJECT_ORG}/${PROJECT_NAME}")

# Project options
OPTION(SUFFIX_SO_VERSION "Suffix library name with its version" ON)

# Project configuration
SET(PROJECT_USE_CMAKE_EXPORT TRUE)
SET(CXX_DISABLE_WERROR TRUE)

# JRL-cmakemodule setup
INCLUDE(cmake/base.cmake)

# Project definition
COMPUTE_PROJECT_ARGS(PROJECT_ARGS LANGUAGES CXX C)
PROJECT(${PROJECT_NAME} ${PROJECT_ARGS})
CHECK_MINIMAL_CXX_STANDARD(11 ENFORCE)

# Project dependencies
ADD_PROJECT_DEPENDENCY(urdfdom REQUIRED)
ADD_PROJECT_DEPENDENCY(gazebo REQUIRED)
ADD_PROJECT_DEPENDENCY(roscpp)
find_package(catkin REQUIRED COMPONENTS roscpp)

if(NOT ${gazebo_VERSION} VERSION_LESS 11)
  CHECK_MINIMAL_CXX_STANDARD(17 ENFORCE)
endif()

# Main Library
SET(${PROJECT_NAME}_HEADERS
  )

SET(${PROJECT_NAME}_SOURCES
  src/SpringPlugin.cc
  )

ADD_LIBRARY(${PROJECT_NAME} SHARED
  ${${PROJECT_NAME}_SOURCES} ${${PROJECT_NAME}_HEADERS})
TARGET_INCLUDE_DIRECTORIES(${PROJECT_NAME} PRIVATE ${GAZEBO_INCLUDE_DIRS} ${catkin_INCLUDE_DIRS})

IF(SUFFIX_SO_VERSION)
  SET_TARGET_PROPERTIES(${PROJECT_NAME} PROPERTIES SOVERSION ${PROJECT_VERSION})
ENDIF(SUFFIX_SO_VERSION)

INSTALL(TARGETS ${PROJECT_NAME} EXPORT ${TARGETS_EXPORT_NAME} DESTINATION lib)

FOREACH(dir config gazebo meshes robots urdf srdf launch)
  INSTALL(DIRECTORY ${dir} DESTINATION share/${PROJECT_NAME})
ENDFOREACH(dir)

INSTALL(FILES package.xml DESTINATION share/${PROJECT_NAME})
SET(PACKAGE_EXTRA_MACROS "set(TALOS_DATA_PREFIX ${CMAKE_INSTALL_PREFIX})")
